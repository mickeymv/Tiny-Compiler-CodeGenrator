%%
Tiny       -> PROGRAM Name ':' Consts Types Dclns Body Name '.' 	=> "program";
Consts     -> CONST	Const list ';'		=> "consts"
		   -> 					     	=> "consts";
Const      -> Name '='	ConstRVal		=> "const"
		   -> 					     	=> "<null>";
ConstRVal  -> Integer
		   -> Character
		   -> Name;
Types     -> TYPE Typel list ';'		=> "types"
		   -> 					     	=> "types";
Typel     -> Name '=' '(' Enumerations ')' => "type"
		  -> Name '=' Name 				=> "type"
		  -> 					     	=> "<null>";	
Enumerations -> Name list ','			=> "lit";		      
Dclns      -> VAR (Dcln ';')*				=> "dclns"
           -> 					     	=> "dclns";
Dcln       -> Name list ',' ':' Name			=> "var";
Body       -> BEGINX Statement list ';' END      	=> "block";
Statement  -> Name ASSIGNMENT Expression          	=> "assign"
		   -> Name SWAP Name          				=> "swap"
           -> OUTPUT '(' Outputs ')'           	=> "output"
		   -> READ '(' Name list ',' ')'            => "read"
           -> IF Expression THEN Statement
                            ELSE Statement       	=> "if"
           -> IF Expression THEN Statement       	=> "if"
           -> WHILE Expression DO Statement      	=> "while"
           -> Body
		   -> REPEAT Statements UNTIL Expression			=> "repeat"
		   -> FOR Name ASSIGNMENT Expression TO Expression DO Statement => "upto"
		   -> FOR Name ASSIGNMENT Expression DOWNTO Expression DO Statement => "downto"
		   -> EXIT									=> "exit"
		   -> LOOP Statements POOL					=> "loop"
		   
		   -> CASE Expression OF Case_Clause list ';' Otherwise END	=> "case"
		   ->                                        	=> "<null>";
			
Case_Clause	->	Case_Literal ':' Statement			=>	"case_clause"
			->										=>	"<null>";

Case_Literal ->	CaseT RANGE CaseT		=>	".."
			->	CaseT	;
			
CaseT		-> Name
			-> Character
			-> Integer;
		   
Otherwise	-> ';' OTHERWISE Statement					=> "otherwise"
			-> ';' OTHERWISE Statement	';'				=> "otherwise"
			-> ';';
	   
Statements	->	Statement list ';' ;
	   
Outputs -> 	 Outpute list ','; 
Outpute			-> STRING                               => "<string>"
				-> Expression;

Expression -> Term	                         		
	   -> Term LTE Term				=> "<="
	   -> Term '=' Term				=> "="
	   -> Term NOTEQ Term			=> "<>"
	   -> Term GTE Term				=> ">="
	   -> Term LT Term				=> "<"
	   -> Term GT Term				=> ">";

Term       -> Bina 
			-> Term '-' Bina         => "-"
			-> Term OR Bina         => "or"
	   -> Term '+' Bina				=> "+";
	   
Bina   -> Unary	
		-> Bina '*' Unary				=> "*"
		-> Bina '/' Unary				=> "/"
		-> Bina AND Unary				=> "and"
   		-> Bina MOD Unary				=> "mod";

Unary    -> '-' Unary                            	=> "-"
		-> '+' Unary                            	
		-> NOT Unary                            	=> "not"
		-> Expo;

Expo -> Primary EXP Expo 				=> "**"
	 -> Primary;

Primary    -> EOFFN						=> "eof"
           -> Name
           -> INTEGER_NUM				=> "<integer>"
		   -> CHARLIT					=> "<char>"
		   -> Intrins
		   -> CHR '(' Expression ')'	=> "chr"
           -> '(' Expression ')';
		   
Intrins		-> SUCC '(' Expression ')'	=> "succ"
			-> PRED '(' Expression ')'	=> "pred"
			-> ORD '(' Expression ')'		=> "ord";

Character  -> CHARLIT					=> "<char>"; 		   
		   
Integer		-> 		INTEGER_NUM			=> "<integer>"   ;

Name       -> IDENTIFIER				=> "<identifier>";

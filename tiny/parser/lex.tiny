%{
#include <stdio.h>
#include <string.h>
#include <assert.h>
#include <Tokenizer.h>
#include "y.tab.h"

static int line = 1;
static int column = 1;
int rule(int token);
int node(int token);
void yyerror(char* message);
int debug_tokenizer = 0;

%}
%x COMM1
COMM2   #.*\n
IDENT	[_a-zA-Z][_a-zA-Z0-9]*
INT	[0-9]+
WHITE   [ \t\v\f]*
LIT     '[^\']'
STR     \"[^\"]*\"
%%

{WHITE}    	{ column += yyleng; }
\n         	{ column = 1; line++; }
"program"   { return rule(PROGRAM); }
"function"   { return rule(FUNCTION); }
"return"   { return rule(RETURN); }
"var" 		{ return rule(VAR); }
"const" 	{ return rule(CONST); }
"type" 		{ return rule(TYPE); }
"begin" 	{ return rule(BEGINX); }
"end" 		{ return rule(END); }
":=" 		{ return rule(ASSIGNMENT); }
":=:" 		{ return rule(SWAP); }
"output" 	{ return rule(OUTPUT); }
"if" 		{ return rule(IF); }
"then" 		{ return rule(THEN); }
"else" 		{ return rule(ELSE); }
"while" 	{ return rule(WHILE); }
"do" 		{ return rule(DO); }
"<=" 		{ return rule(LTE); }
">=" 		{ return rule(GTE); }
"<" 		{ return rule(LT); }
">" 		{ return rule(GT); }
"<>" 		{ return rule(NOTEQ); }
"read" 		{ return rule(READ); }
"or" 		{ return rule(OR); }
"not" 		{ return rule(NOT); }
"and" 		{ return rule(AND); }
"mod" 		{ return rule(MOD); }
"**" 		{ return rule(EXP); }
"eof" 		{ return rule(EOFFN); }
"repeat" 	{ return rule(REPEAT); }
"until" 	{ return rule(UNTIL); }
"loop" 		{ return rule(LOOP); }
"pool" 		{ return rule(POOL); }
"exit" 		{ return rule(EXIT); }
"succ" 		{ return rule(SUCC); }
"pred" 		{ return rule(PRED); }
"ord" 		{ return rule(ORD); }
"chr" 		{ return rule(CHR); }
"for" 		{ return rule(FOR); }
"to" 		{ return rule(TO); }
"downto" 	{ return rule(DOWNTO); }
"case" 		{ return rule(CASE); }
"of" 		{ return rule(OF); }
".." 		{ return rule(RANGE); }
"otherwise" { return rule(OTHERWISE); }
{INT}		{ return node(INTEGER_NUM); }
{IDENT}		{ return node(IDENTIFIER); }
{LIT}		{ return node(CHARLIT); }
{STR}		{ return node(STRING); }
"{"		{ column += yyleng; BEGIN( COMM1 ); }
<COMM1>[^}\n]+  { column += yyleng; }
<COMM1>"}"	{ column += yyleng; BEGIN( INITIAL ); }
<COMM1>\n	{ column = 1; line++; }
{COMM2}         { column = 1; line++; }
":"             { return rule(yytext[0]); }
";"             { return rule(yytext[0]); }
"."             { return rule(yytext[0]); }
","             { return rule(yytext[0]); }
"("             { return rule(yytext[0]); }
")"             { return rule(yytext[0]); }
"+"             { return rule(yytext[0]); }
"-"             { return rule(yytext[0]); }
"*"             { return rule(yytext[0]); }
"/"             { return rule(yytext[0]); }
"="             { return rule(yytext[0]); }
.               { yyerror("unrecognized char");
                  printf("-->%s<--\n",yytext);
                  column++; }  
%%

int rule(int token)
{
    if (debug_tokenizer) {
	printf("string:%s, token: %d, line: %d, column: %d\n",
	       yytext,token,line,column);
    }

    yylval.info.line = line;
    yylval.info.column = column;
    column += yyleng;

    yylval.info.string = malloc(yyleng+1);
    assert(yylval.info.string);
    strcpy(yylval.info.string, yytext); 

    yylval.info.makenode = 0;

    return token;
}

int node(int token)
{
    int tok = rule(token);
    yylval.info.makenode = 1;
    return tok;
}

void yyerror(char* message)
{
    printf("****** %s @line %d, column %d\n",
	   message,line,column);
}

